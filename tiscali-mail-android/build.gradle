buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.google.gms:google-services:3.0.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

apply plugin: 'com.neenbedankt.android-apt'

if (!rootProject.optimizeForDevelopment) {
    apply from: '../gradle/plugins/checkstyle-android.gradle'
    apply from: '../gradle/plugins/findbugs-android.gradle'
}

if (rootProject.testCoverage) {
    apply plugin: 'jacoco'
}

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

//noinspection GroovyAssignabilityCheck
configurations.all {
    resolutionStrategy {
        force "com.android.support:support-annotations:${androidSupportLibraryVersion}"
    }
}

dependencies {
    compile project(':k9mail-library')
    compile project(':plugins:Android-PullToRefresh:library')
    compile project(':plugins:HoloColorPicker')
    compile project(':plugins:openpgp-api-lib:openpgp-api')
    compile project(':sdk-release')
    compile "com.android.support:support-v4:${androidSupportLibraryVersion}"
    compile "com.android.support:appcompat-v7:${androidSupportLibraryVersion}"
    compile "com.android.support:recyclerview-v7:${androidSupportLibraryVersion}"
    compile "com.android.support:cardview-v7:${androidSupportLibraryVersion}"
    compile "com.android.support:design:${androidSupportDesignVersion}"
//    compile files('libs/SmartAdServer-Android-SDK-6.6.2.jar')
    //    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    //    compile 'io.reactivex.rxjava2:rxjava:2.0.4'
    //    compile 'com.google.android.gms:play-services-appindexing:8.4.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }
    compile 'commons-io:commons-io:2.4'
    compile 'net.sourceforge.htmlcleaner:htmlcleaner:2.16'
    compile 'de.cketti.library.changelog:ckchangelog:1.2.1'
    compile 'com.splitwise:tokenautocomplete:2.0.7'
    compile 'de.cketti.safecontentresolver:safe-content-resolver-v14:0.9.0'
    compile 'com.github.amlcurran.showcaseview:library:5.4.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.google.dagger:dagger:2.8'
    //    compile 'org.glassfish:javax.annotation:10.0-b28'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'com.squareup.okhttp3:logging-interceptor:3.3.1'
    compile 'com.google.firebase:firebase-messaging:10.0.1'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.webtrekk.webtrekksdk:WebtrekkSDK:4.3.1'
    testCompile "org.robolectric:robolectric:${robolectricVersion}"
    testCompile "org.robolectric:shadows-multidex:${robolectricVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"

    androidTestCompile "junit:junit:${junitVersion}"
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-web:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile "com.android.support:support-v4:${androidSupportLibraryVersion}"
    androidTestCompile "com.android.support:appcompat-v7:${androidSupportLibraryVersion}"
    androidTestCompile "com.android.support:recyclerview-v7:${androidSupportLibraryVersion}"
    androidTestCompile "com.android.support:cardview-v7:${androidSupportLibraryVersion}"
    androidTestCompile "com.android.support:design:${androidSupportDesignVersion}"

    provided 'javax.annotation:jsr250-api:1.0'
    apt 'com.google.dagger:dagger-compiler:2.8'
}

android {
    compileSdkVersion androidCompileSdkVersion.toInteger()
    buildToolsVersion androidBuildToolsVersion

    int K9_VERSION_CODE = 23113
    String K9_VERSION_NAME = '5.114'

    int VERSION_CODE = 204
    String VERSION_NAME = "2.0.4.1"

    defaultConfig {
        applicationId "com.tiscali.appmail"
        testApplicationId "com.tiscali.appmail.tests"

        versionCode VERSION_CODE
        versionName VERSION_NAME

        minSdkVersion 15
        targetSdkVersion androidTargetSdkVersion.toInteger()

        vectorDrawables.generatedDensities = ['mdpi', 'hdpi', 'xhdpi', 'xxhdpi']

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled = true
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            if (project.hasProperty('storeFile')) {
                signingConfig signingConfigs.release
            }

            buildConfigField "boolean", "DEVELOPER_MODE", "false"

            applicationVariants.all { variant ->
                variant.outputs.each { output ->

                    // apk file names
                    if (output.outputFile.name.endsWith(".apk")) {
                        output.outputFile = new File(output.outputFile.parent, "tiscali.it-" + variant.versionName + "-rc-" + K9_VERSION_NAME + ".apk")
                    }
                }
            }
        }

        debug {

            testCoverageEnabled rootProject.testCoverage

            buildConfigField "boolean", "DEVELOPER_MODE", "true"
        }
    }

    dexOptions {
        jumboMode = true
        javaMaxHeapSize "2048M"
    }

    // Do not abort build if lint finds errors
    lintOptions {
        abortOnError false
        lintConfig file("$rootProject.projectDir/config/lint/lint.xml")
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }


    sourceSets {
        androidTest {
            setRoot('src/androidTest/')
            java.srcDirs = ['src/androidTest/java/']
        }
    }

}

if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
}
if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
}

apply plugin: 'com.google.gms.google-services'